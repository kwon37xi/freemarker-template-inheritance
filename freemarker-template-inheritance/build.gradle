// The original source file has been modified by Matteo Silvestri 
// <matteosilv@gmail.com> on date 2017-10-26
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    compile 'org.freemarker:freemarker:2.3.19'

    testCompile 'junit:junit:4.11'
}

// from http://central.sonatype.org/pages/gradle.html

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    logger.info('Initializing signing and uploadArchives')

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name "${project.name}"
                    packaging 'jar'
                    description 'Freemarker template inheritance.'
                    url 'https://github.com/matteosilv/freemarker-template-inheritance'

                    scm {
                        connection 'scm:git:https://github.com/matteosilv/freemarker-template-inheritance.git'
                        url 'https://github.com/matteosilv/freemarker-template-inheritance'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'kwon37xi'
                            name 'KwonNam Son'
                            email 'kwon37xi@gmail.com'
                        }
                        developer {
                            id 'matteosilv'
                            name 'Matteo Silvestri'
                            email 'matteosilv@gmail.com'
                        }
                    }
                }
            }
        }
    }
}
